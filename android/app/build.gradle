plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 35
    namespace "fr.fabrique.social.gouv.pass_emploi_app"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        // Required for flutter_local_notifications dependency
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    defaultConfig {
        applicationId "fr.fabrique.social.gouv.passemploi"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'fr.fabrique.social.gouv.passemploi'
        ]
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "brand", "env"

    productFlavors {
        brsa {
            dimension "brand"
            applicationId "fr.fabrique.social.gouv.passemploi.rsa"
        }
        cej {
            dimension "brand"
            applicationId "fr.fabrique.social.gouv.passemploi"
        }
        staging {
            dimension "env"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        prod {
            dimension "env"
        }
    }

    lint {
        checkReleaseBuilds false
    }

    // Required to make flutter tool not exit with an error on aab build with multiple flavors
    // https://github.com/flutter/flutter/issues/65264#issuecomment-707013948
    applicationVariants.all { variant ->
        variant.outputs.all {
            def taskSuffix = variant.name.capitalize()
            def bundleTaskName = "bundle${taskSuffix}"
            if (tasks.findByName(bundleTaskName)) {
                def copyAABTask = tasks.create(name: "copyAndRenameAab${taskSuffix}", type: Copy) {
                    def bundleBuildDir = "${buildDir}/outputs/bundle"
                    def flavorsCapitalized = variant.productFlavors.name.withIndex().collect { flavor, index -> index == 0 ? flavor : flavor.capitalize() }
                    def originalDirPath = "${flavorsCapitalized.join()}${variant.buildType.name.capitalize()}"
                    def originalFileName = "${project.archivesBaseName}-${variant.productFlavors.name.join('-')}-${variant.buildType.name}.aab"
                    from "${bundleBuildDir}/${originalDirPath}/${originalFileName}"
                    into "${bundleBuildDir}/${variant.buildType.name}/"
                    rename("${originalFileName}", "app.aab")
                }
                tasks[bundleTaskName].finalizedBy = [copyAABTask]
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.9.20"
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'

    // // Required for CVM
    // implementation fileTree(include: ['matrixlib-1.2.107.aar'], dir: 'libs')
    // implementation 'org.matrix.android:matrix-android-sdk2:1.5.4'
    // implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    // implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    // implementation "io.reactivex.rxjava3:rxjava:3.0.1"
    // implementation "io.reactivex.rxjava3:rxkotlin:3.0.0"
    // implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
}


apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
